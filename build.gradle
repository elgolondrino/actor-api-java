apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'im.actor'
version = '0.5.0'

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

project.version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:2.5.0'
    compile 'com.droidkit:actors:0.6.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Actor API for Java'
                    packaging 'jar'
                    description 'Actor Api for Java'
                    url 'https://github.com/actorapp/actor-api-java'

                    scm {
                        url 'scm:git@github.com:actorapp/actor-api-java.git'
                        connection 'scm:git@github.com:actorapp/actor-api-java.git'
                        developerConnection 'scm:git@github.com:actorapp/actor-api-java.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'ex3ndr'
                            name 'Stepan Korshakov'
                        }
                    }
                }
            }
        }
    }
}